import java.text.SimpleDateFormat

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
}

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    compileJava.options.encoding('UTF-8')
    withJavadocJar()
    withSourcesJar()
}

def undertowVersion = '2.3.19.Final'

group = 'io.github.amayaframework'
archivesBaseName = 'amaya-undertow'
version = (System.getenv('UNDERTOW_VERSION') ?: '1.0.0') + '-' + undertowVersion

repositories {
    mavenCentral()
}

dependencies {
    // TODO Remove dev deps
	implementation group: 'io.github.amayaframework', name: 'amaya-core', version: '3.6.0'
    // Tests
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'
    // Tokenizer
    implementation group: 'io.github.amayaframework', name: 'amaya-tokenize', version: '2.0.0'
    // Amaya modules
    compileOnly group: 'io.github.amayaframework', name: 'amaya-http', version: '3.0.1'
    compileOnly group: 'io.github.amayaframework', name: 'amaya-environment', version: '2.0.0'
    compileOnly group: 'io.github.amayaframework', name: 'amaya-options', version: '2.0.0'
    compileOnly group: 'io.github.amayaframework', name: 'amaya-service', version: '2.0.1'
    compileOnly group: 'io.github.amayaframework', name: 'amaya-context', version: '3.1.0'
    compileOnly group: 'io.github.amayaframework', name: 'amaya-server', version: '2.4.0'
    // Undertow core
    api group: 'io.undertow', name: 'undertow-core', version: undertowVersion
    // Undertow servlets
    api group: 'io.undertow', name: 'undertow-servlet', version: undertowVersion
	// Undertow websockets
	compileOnly group: 'io.undertow', name: 'undertow-websockets-jsr', version: undertowVersion
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        var date = new Date()
        var javaVersion = System.getProperty("java.version")
        var vmVendor = System.getProperty("java.vm.vendor")
        var vmVersion = System.getProperty("java.vm.version")

        attributes(
                'Created-By': String.format("%s (%s %s)", javaVersion, vmVendor, vmVersion),
                'Gradle-Version': "Gradle $gradle.gradleVersion",
                'Build-Date': new SimpleDateFormat("yyyy-MM-dd").format(date),
                'Build-Time': new SimpleDateFormat("HH:mm:ss.SSSZ").format(date),
                'Built-By': 'Roman Bakaldin',
                'Bundle-Name': 'Amaya Undertow Server',
                'Bundle-Version': archiveVersion,
                'Bundle-SymbolicName': archivesBaseName,
                'Implementation-Title': archivesBaseName,
                'Implementation-Vendor': 'github.com/amayaframework',
                'Implementation-Version': archiveVersion,
                'Specification-Title': archivesBaseName,
                'Specification-Vendor': 'github.com/amayaframework',
                'Specification-Version': archiveVersion
        )
    }
}

signing {
    sign publishing.publications
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            // Specify artifacts
            artifactId = archivesBaseName
            groupId = group
            version = version
            from components.java
            // Configure pom
            pom {
                name.set(archivesBaseName)
                description.set('The amaya-server implementation is based on Undertow.')
                url.set('https://github.com/AmayaFramework/amaya-undertow')

                organization {
                    name.set('io.github.amayaframework')
                    url.set('https://github.com/AmayaFramework')
                }

                issueManagement {
                    system.set('GitHub')
                    url.set('https://github.com/AmayaFramework/amaya-undertow/issues')
                }

                licenses {
                    license {
                        name.set('Apache License 2.0')
                        url.set('https://github.com/AmayaFramework/amaya-undertow/blob/main/LICENSE')
                    }
                }

                scm {
                    url.set('https://github.com/AmayaFramework/amaya-undertow')
                    connection.set('scm:https://github.com/AmayaFramework/amaya-undertow.git')
                    developerConnection.set('scm:https://github.com/AmayaFramework/amaya-undertow.git')
                }

                developers {
                    developer {
                        id.set('RomanQed')
                        name.set('Roman Bakaldin')
                        email.set('gbakaldin@gmail.com')
                    }
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {
            username = centralUsername
            password = centralPassword
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
            snapshotRepositoryUrl.set(uri("https://central.sonatype.com/repository/maven-snapshots/"))
        }
    }
}
